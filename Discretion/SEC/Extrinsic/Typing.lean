import Mathlib.CategoryTheory.Monoidal.Free.Basic
import Discretion.Wk.List

import Discretion.SEC.Signature
import Discretion.SEC.Untyped

open CategoryTheory

open CategoryTheory.MonoidalCategory

namespace SEC

open FreeSignature

namespace Term

variable {Ï„} [FreeSignature Ï„]

inductive Wf : List (Ty Ï„) â†’ Term Ï„ â†’ Ty Ï„ â†’ Prop
  | var {Î“ i A} (hÎ“ : i < Î“.length) : Î“[i] = A â†’ Wf Î“ (.var i) A
  | op {Î“ f a A B} (hA : f.src = A) (hB : f.trg = B) : Wf Î“ a A â†’ Wf Î“ (.op f a) B
  | letâ‚ {Î“ a b A B} : Wf Î“ a A â†’ Wf (A::Î“) b B â†’ Wf Î“ (.letâ‚ a b) B
  | unit {Î“} : Wf Î“ .unit (ğŸ™_ _)
  | pair {Î“ a b A B} : Wf Î“ a A â†’ Wf Î“ b B â†’ Wf Î“ (.pair a b) (A âŠ— B)
  | letâ‚‚ {Î“ a c A B C} : Wf Î“ a (A âŠ— B) â†’ Wf (B::A::Î“) c C â†’ Wf Î“ (.letâ‚‚ a c) C

-- attribute [class] Wf

-- attribute [instance] var unit

-- instance Wf.op_f (Î“ f a A B) [hf : Untyped.IsFn f A B] [ha : Wf Î“ a A]
--   : Wf (Ï„ := Ï„) Î“ (.op f a) B := .op hf.src_eq hf.trg_eq ha

-- instance Wf.pair_f (Î“) (a b) (A B) [ha : Wf Î“ a A] [hb : Wf Î“ b B]
--   : Wf (Ï„ := Ï„) Î“ (.pair a b) (A âŠ— B) := .pair ha hb

def inferTy (Î“ : List (Ty Ï„)) : Term Ï„ â†’ Ty Ï„
  | .var i => if h : i < Î“.length then Î“[i] else .unit
  | .op f a => f.trg
  | .letâ‚ a b => b.inferTy ((a.inferTy Î“)::Î“)
  | .pair a b => a.inferTy Î“ âŠ— b.inferTy Î“
  | .letâ‚‚ a b => match a.inferTy Î“ with
    | .tensor A B => b.inferTy (B::A::Î“)
    | _ => .unit
  | _ => .unit

theorem Wf.inferTy_eq {Î“} {t : Term Ï„} {A} (hA : Wf Î“ t A)
  : t.inferTy Î“ = A := by induction hA <;> simp [inferTy, *]

theorem Wf.unique {Î“} {t : Term Ï„} {A B}
  (hA : Wf Î“ t A) (hB : Wf Î“ t B) : A = B := by rw [<-hA.inferTy_eq, <-hB.inferTy_eq]

def Wf.invalid {Î“ A} (h : Wf (Ï„ := Ï„) Î“ .invalid A) {Î±} : Î± := by cases h

def inferLeft (Î“ : List (Ty Ï„)) (a : Term Ï„) : Ty Ï„ := match a.inferTy Î“ with
  | .tensor A _ => A
  | _ => .unit

def inferRight (Î“ : List (Ty Ï„)) (a : Term Ï„) : Ty Ï„ := match a.inferTy Î“ with
  | .tensor _ B => B
  | _ => .unit

@[simp]
theorem Wf.var_lt_length {Î“ i A} (h : Wf (Ï„ := Ï„) Î“ (.var i) A)
  : i < Î“.length := by cases h; assumption

@[simp]
theorem Wf.var_getElem_eq {Î“ i A} (h : Wf (Ï„ := Ï„) Î“ (.var i) A)
  : Î“[i]'h.var_lt_length = A := by cases h; assumption

theorem Wf.op_src_eq {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : f.src = a.inferTy Î“ := by cases h with | op h _ ha => convert h; rw [ha.inferTy_eq]

theorem Wf.op_trg_eq {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : f.trg = B := by cases h with | op _ h _ => exact h

theorem Wf.op_arg {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : Wf Î“ a f.src := by cases h with | op h _ ha => exact h â–¸ ha

theorem Wf.unit_ty_eq {Î“} (h : Wf (Ï„ := Ï„) Î“ .unit A) : A = .unit := by cases h; rfl

theorem Wf.pair_ty_eq {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C)
  : C = a.inferTy Î“ âŠ— b.inferTy Î“
  := by cases h with | pair ha hb => rw [ha.inferTy_eq, hb.inferTy_eq]

theorem Wf.pair_left {Î“ a b A B} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) (A âŠ— B))
  : Wf Î“ a A := by cases h; assumption

theorem Wf.pair_right {Î“ a b A B} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) (A âŠ— B))
  : Wf Î“ b B := by cases h; assumption

theorem Wf.pair_left' {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C) : Wf Î“ a (a.inferTy Î“)
  := (h.pair_ty_eq â–¸ h).pair_left

theorem Wf.pair_right' {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C) : Wf Î“ b (b.inferTy Î“)
  := (h.pair_ty_eq â–¸ h).pair_right

theorem Wf.letâ‚_bind {Î“ a b B} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚ a b) B)
  : Wf Î“ a (a.inferTy Î“) := by cases h with | letâ‚ ha hb => convert ha; rw [ha.inferTy_eq]

theorem Wf.letâ‚_expr {Î“ a b B} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚ a b) B)
  : Wf (a.inferTy Î“::Î“) b B := by cases h with | letâ‚ ha hb => convert hb; rw [ha.inferTy_eq]

theorem Wf.letâ‚‚_bind {Î“ a c C} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚‚ a c) C)
  : Wf Î“ a (a.inferLeft Î“ âŠ— a.inferRight Î“) := by
  cases h with | letâ‚‚ ha hb =>
    simp only [inferLeft, inferRight]
    split
    case h_1 t => rw [ha.inferTy_eq] at t; cases t; exact ha
    case h_2 t => exact (t _ _ ha.inferTy_eq).elim

theorem Wf.letâ‚‚_expr {Î“ a c C} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚‚ a c) C)
  : Wf (a.inferRight Î“::a.inferLeft Î“::Î“) c C := by
  cases h with | letâ‚‚ ha hb =>
    simp only [inferLeft, inferRight]
    split
    case h_1 t => rw [ha.inferTy_eq] at t; cases t; exact hb
    case h_2 t => exact (t _ _ ha.inferTy_eq).elim

-- TODO: set as default eliminator?
@[elab_as_elim]
def Wf.induction' {motive : âˆ€ {Î“ t A}, Wf (Ï„ := Ï„) Î“ t A â†’ Sort u}
  (var : âˆ€ {Î“ i A} (hÎ“ : i < Î“.length) (hA : Î“[i] = A), motive (.var hÎ“ hA))
  (op : âˆ€ {Î“} {f : Untyped.Inst Ï„} {a A B} (hA : f.src = A) (hB : f.trg = B) (ha : Wf Î“ a A),
    motive ha â†’ motive (.op hA hB ha))
  (letâ‚ : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf (A::Î“) b B),
    motive ha â†’ motive hb â†’ motive (.letâ‚ ha hb))
  (unit : âˆ€ {Î“}, motive (Î“ := Î“) .unit)
  (pair : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf Î“ b B),
    motive ha â†’ motive hb â†’ motive (.pair ha hb))
  (letâ‚‚ : âˆ€ {Î“ a c A B C} (ha : Wf Î“ a (A âŠ— B)) (hc : Wf (B::A::Î“) c C),
    motive ha â†’ motive hc â†’ motive (.letâ‚‚ ha hc))
  {Î“ t A} (h : Wf Î“ t A) : motive h := match t, A, h with
  | .var _, _, h => var h.var_lt_length h.var_getElem_eq
  | .op _ _, _, h => op rfl h.op_trg_eq h.op_arg (induction' var op letâ‚ unit pair letâ‚‚ h.op_arg)
  | .letâ‚ _ _, _, h => letâ‚ h.letâ‚_bind h.letâ‚_expr
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚_bind)
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚_expr)
  | .unit, .unit, _ => unit
  | .pair _ _, .tensor _ _, h => pair h.pair_left h.pair_right
    (induction' var op letâ‚ unit pair letâ‚‚ h.pair_left)
    (induction' var op letâ‚ unit pair letâ‚‚ h.pair_right)
  | .letâ‚‚ _ _, _, h => letâ‚‚ h.letâ‚‚_bind h.letâ‚‚_expr
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚‚_bind)
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚‚_expr)

@[elab_as_elim]
def Wf.cases' {motive : âˆ€ {Î“ t A}, Wf (Ï„ := Ï„) Î“ t A â†’ Sort u}
  (var : âˆ€ {Î“ i A} (hÎ“ : i < Î“.length) (hA : Î“[i] = A), motive (.var hÎ“ hA))
  (op : âˆ€ {Î“} {f : Untyped.Inst Ï„} {a A B} (hA : f.src = A) (hB : f.trg = B) (ha : Wf Î“ a A),
    motive (.op hA hB ha))
  (letâ‚ : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf (A::Î“) b B),
    motive (.letâ‚ ha hb))
  (unit : âˆ€ {Î“}, motive (Î“ := Î“) .unit)
  (pair : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf Î“ b B),
    motive (.pair ha hb))
  (letâ‚‚ : âˆ€ {Î“ a c A B C} (ha : Wf Î“ a (A âŠ— B)) (hc : Wf (B::A::Î“) c C),
    motive (.letâ‚‚ ha hc))
  {Î“ t A} (h : Wf Î“ t A) : motive h := match t, A, h with
  | .var _, _, h => var h.var_lt_length h.var_getElem_eq
  | .op _ _, _, h => op rfl h.op_trg_eq h.op_arg
  | .letâ‚ _ _, _, h => letâ‚ h.letâ‚_bind h.letâ‚_expr
  | .unit, .unit, _ => unit
  | .pair _ _, .tensor _ _, h => pair h.pair_left h.pair_right
  | .letâ‚‚ _ _, _, h => letâ‚‚ h.letâ‚‚_bind h.letâ‚‚_expr

theorem Wf.wk {Î“ Î”} {Ï : â„• â†’ â„•} (hÏ : List.IsRen Î“ Î” Ï) {t : Term Ï„} {A}
  (h : Wf Î” t A) : Wf Î“ (t.wk Ï) A := by
  induction h generalizing Î“ Ï with
  | var hi => have h := hÏ.getElem_eq _ hi; constructor; rw [h]; assumption
  | _ => constructor <;> apply_assumption

theorem Wf.wk_f {Î“ Î”} (Ï : â„• â†’ â„•) [hÏ : List.IsRen Î“ Î” Ï] {t : Term Ï„} {A}
  (h : Wf Î” t A) : Wf Î“ (t.wk Ï) A := h.wk hÏ

theorem Wf.unwk {Î“ Î”} {Ï : â„• â†’ â„•} (hÏ : List.IsRen Î“ Î” Ï) {t : Term Ï„} {A}
  (h : Wf Î“ (t.wk Ï) A) (ht : t.fvi â‰¤ Î”.length) : Wf Î” t A := by
  induction t generalizing Î“ Î” Ï A <;> cases h
  case var hÎ“ ha => apply var; rw [<-hÏ.getElem_eq _ ht, ha]
  all_goals {
    simp [fvi] at ht
    constructor
    all_goals {
      apply_assumption
      <;> (repeat apply Î»hÏ => List.IsRen.lift (hÏ := hÏ))
      <;> first | assumption | ((try simp only [List.length_cons]); omega)
    }
  }

theorem Wf.unwk_f {Î“ Î”} (Ï : â„• â†’ â„•) [hÏ : List.IsRen Î“ Î” Ï] {t : Term Ï„} {A}
  (h : Wf Î“ (t.wk Ï) A) (ht : t.fvi â‰¤ Î”.length) : Wf Î” t A := h.unwk hÏ ht

theorem Wf.fvi {Î“} {t : Term Ï„} {A} (h : Wf Î“ t A) : t.fvi â‰¤ Î“.length := by
  induction h <;> simp [Term.fvi] at * <;> omega

theorem Wf.unwk_b {Î“ Î”} {Ï : â„• â†’ â„•}
  (hÏ : List.IsRen Î“ Î” Ï) (hÏ' : BoundedFrom Î”.length Î“.length Ï)
  {t : Term Ï„} {A} (h : Wf Î“ (t.wk Ï) A) : Wf Î” t A := unwk_f Ï h (t.fvi_bounded_from_f Ï h.fvi)

theorem Wf.unwk_bf {Î“ Î”} (Ï : â„• â†’ â„•)
  [hÏ : List.IsRen Î“ Î” Ï] [hÏ' : BoundedFrom Î”.length Î“.length Ï]
  {t : Term Ï„} {A} (h : Wf Î“ (t.wk Ï) A) : Wf Î” t A := h.unwk_b hÏ hÏ'

theorem Wf.wk_iff {Î“ Î”} {Ï : â„• â†’ â„•}
  (hÏ : List.IsRen Î“ Î” Ï) (hÏ' : BoundedFrom Î”.length Î“.length Ï)
  (t : Term Ï„) (A) : Wf Î“ (t.wk Ï) A â†” Wf Î” t A := âŸ¨Î»h => h.unwk_b hÏ hÏ', Î»h => h.wk hÏâŸ©

theorem Wf.wk_iff_f {Î“ Î”} (Ï : â„• â†’ â„•)
  [hÏ : List.IsRen Î“ Î” Ï] [hÏ' : BoundedFrom Î”.length Î“.length Ï]
  (t : Term Ï„) (A) : Wf Î“ (t.wk Ï) A â†” Wf Î” t A := wk_iff hÏ hÏ' t A

theorem Wf.wk0 {Î“} {t : Term Ï„} {A B}
  (h : Wf Î“ t A) : Wf (B::Î“) (wk0 t) A := h.wk_f .succ

theorem Wf.unwk0 {Î“} {t : Term Ï„} {A B}
  (h : Wf (B::Î“) (wk0 t) A) : Wf Î“ t A := h.unwk_bf .succ

theorem Wf.wk0_iff {Î“} {t : Term Ï„} {A B}
  : Wf (B::Î“) (wk0 t) A â†” Wf Î“ t A := wk_iff_f .succ t A

theorem Wf.wk1 {Î“} {t : Term Ï„} {A B C}
  (h : Wf (C::Î“) t A) : Wf (C::B::Î“) (wk1 t) A := h.wk_f (Nat.liftWk .succ)

theorem Wf.unwk1 {Î“} {t : Term Ï„} {A B C}
  (h : Wf (C::B::Î“) (wk1 t) A) : Wf (C::Î“) t A := h.unwk_bf (Nat.liftWk .succ)

theorem Wf.wk1_iff {Î“} {t : Term Ï„} {A B C}
  : Wf (C::B::Î“) (wk1 t) A â†” Wf (C::Î“) t A := wk_iff_f (Nat.liftWk .succ) t A

inductive WfqD : (Î“ : List (Ty Ï„)) â†’ Vector' EQuant Î“.length â†’ Term Ï„ â†’ Ty Ï„ â†’ Type _
  | var {Î“ qs i A} : Î“.QVar qs i A â†’ WfqD Î“ qs (.var i) A
  | op {Î“ qs f a A B} (hA : f.src = A) (hB : f.trg = B) : WfqD Î“ qs a A â†’ WfqD Î“ qs (.op f a) B
  | letâ‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b A B}
    : ql + qr â‰¤ qs â†’ WfqD Î“ ql a A â†’ WfqD (A::Î“) (qr.cons (quant A)) b B â†’ WfqD Î“ qs (.letâ‚ a b) B
  | unit {Î“ qs} : 0 â‰¤ qs â†’ WfqD Î“ qs .unit (ğŸ™_ _)
  | pair {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b A B}
    : ql + qr â‰¤ qs â†’ WfqD Î“ ql a A â†’ WfqD Î“ qr b B â†’ WfqD Î“ qs (.pair a b) (A âŠ— B)
  | letâ‚‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a c A B C}
    : ql + qr â‰¤ qs
    â†’ WfqD Î“ ql a (A âŠ— B)
    â†’ WfqD (B::A::Î“) ((qr.cons â†‘(quant A)).cons â†‘(quant B)) c C
    â†’ WfqD Î“ qs (.letâ‚‚ a c) C

open BoundedOn

def WfqD.wk {Î“ Î” qÎ“ qÎ”} {Ï : â„• â†’ â„•} (hÏ : List.IsQRen qÎ“ qÎ” Ï) {t : Term Ï„} {A}
  (h : WfqD Î” qÎ” t A) : WfqD Î“ qÎ“ (t.wk Ï) A := match h with
  | .var h => .var (h.wk hÏ)
  | .op hA hB h => .op hA hB (h.wk hÏ)
  | .letâ‚ hq ha hb =>
    .letâ‚
      (le_pvSum_of_le_sum _ _ Ï _ _ _ _ hq hÏ.quant_le_sum)
      (ha.wk (List.IsQRen.of_pvSum _ _ _))
      (hb.wk ((List.IsQRen.of_pvSum _ _ _).lift _ _))
  | .unit h => .unit (hÏ.le_zero _ _ _ h)
  | .pair hq ha hb =>
    .pair
      (le_pvSum_of_le_sum _ _ Ï _ _ _ _ hq hÏ.quant_le_sum)
      (ha.wk (List.IsQRen.of_pvSum _ _ _))
      (hb.wk (List.IsQRen.of_pvSum _ _ _))
  | letâ‚‚ hq ha hb =>
    .letâ‚‚
      (le_pvSum_of_le_sum _ _ Ï _ _ _ _ hq hÏ.quant_le_sum)
      (ha.wk (List.IsQRen.of_pvSum _ _ _))
      (hb.wk (((List.IsQRen.of_pvSum _ _ _).lift _ _).lift _ _))

-- TODO: inductive version with better defeq?
def WfqD.mono {Î“ qÎ“ qÎ“'} (hqÎ“ : qÎ“ â‰¤ qÎ“') (h : WfqD (Ï„ := Ï„) Î“ qÎ“ a A) : WfqD Î“ qÎ“' a A
  := by convert h.wk (List.IsQRen.id_of_le hqÎ“); simp [Term.wk_id]

inductive Wfq : (Î“ : List (Ty Ï„)) â†’ Vector' EQuant Î“.length â†’ Term Ï„ â†’ Ty Ï„ â†’ Prop
  | var {Î“ qs i A} : Î“.QVar qs i A â†’ Wfq Î“ qs (.var i) A
  | op {Î“ qs f a A B} (hA : f.src = A) (hB : f.trg = B) : Wfq Î“ qs a A â†’ Wfq Î“ qs (.op f a) B
  | letâ‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b A B}
    : ql + qr â‰¤ qs â†’ Wfq Î“ ql a A â†’ Wfq (A::Î“) (qr.cons (quant A)) b B â†’ Wfq Î“ qs (.letâ‚ a b) B
  | unit {Î“ qs} : 0 â‰¤ qs â†’ Wfq Î“ qs .unit (ğŸ™_ _)
  | pair {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b A B}
    : ql + qr â‰¤ qs â†’ Wfq Î“ ql a A â†’ Wfq Î“ qr b B â†’ Wfq Î“ qs (.pair a b) (A âŠ— B)
  | letâ‚‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a c A B C}
    : ql + qr â‰¤ qs
    â†’ Wfq Î“ ql a (A âŠ— B)
    â†’ Wfq (B::A::Î“) ((qr.cons â†‘(quant A)).cons â†‘(quant B)) c C
    â†’ Wfq Î“ qs (.letâ‚‚ a c) C

theorem Wfq.var_iff {Î“ qs i A}
  : Wfq (Ï„ := Ï„) Î“ qs (.var i) A â†” Î“.QVar qs i A := âŸ¨Î» | .var h => h, .varâŸ©

theorem Wfq.op_iff {Î“ qs f a B}
  : Wfq (Ï„ := Ï„) Î“ qs (.op f a) B â†” f.trg = B âˆ§ Wfq Î“ qs a f.src := âŸ¨
    Î»| .op hA hB ha => by cases hA; exact âŸ¨hB, haâŸ©,
    Î» âŸ¨hB, haâŸ© => .op rfl hB ha
  âŸ©

theorem Wfq.letâ‚_iff {Î“ qs a b B}
  : Wfq (Ï„ := Ï„) Î“ qs (.letâ‚ a b) B
  â†” âˆƒql qr A, ql + qr â‰¤ qs âˆ§ Wfq Î“ ql a A âˆ§ Wfq (A::Î“) (qr.cons (quant A)) b B := âŸ¨
    Î»| .letâ‚ h ha hb => âŸ¨_, _, _, h, ha, hbâŸ©,
    Î»âŸ¨_, _, _, h, ha, hbâŸ© => .letâ‚ h ha hb
  âŸ©

theorem Wfq.unit_iff {Î“ qs A}
  : Wfq (Ï„ := Ï„) Î“ qs .unit A â†” 0 â‰¤ qs âˆ§ A = (ğŸ™_ _)
  := âŸ¨Î»| .unit h => âŸ¨h, rflâŸ©, Î»âŸ¨h, h'âŸ© => h' â–¸ .unit hâŸ©

theorem Wfq.pair_iff {Î“ qs a b C}
  : Wfq (Ï„ := Ï„) Î“ qs (.pair a b) C
  â†” âˆƒql qr A B, ql + qr â‰¤ qs âˆ§ Wfq Î“ ql a A âˆ§ Wfq Î“ qr b B âˆ§ C = A âŠ— B := âŸ¨
    Î»| .pair h ha hb => âŸ¨_, _, _, _, h, ha, hb, rflâŸ©,
    Î»âŸ¨_, _, _, _, h, ha, hb, hCâŸ© => hC â–¸ .pair h ha hb
  âŸ©

theorem Wfq.letâ‚‚_iff {Î“ qs a c C}
  : Wfq (Ï„ := Ï„) Î“ qs (.letâ‚‚ a c) C
  â†” âˆƒql qr A B, ql + qr â‰¤ qs âˆ§ Wfq Î“ ql a (A âŠ— B)
    âˆ§ Wfq (B::A::Î“) ((qr.cons â†‘(quant A)).cons â†‘(quant B)) c C := âŸ¨
    Î»| .letâ‚‚ h ha hb => âŸ¨_, _, _, _, h, ha, hbâŸ©,
    Î»âŸ¨_, _, _, _, h, ha, hbâŸ© => .letâ‚‚ h ha hb
  âŸ©

-- Nonempty WfqD â†” Wfq

inductive WqD : (Î“ : List (Ty Ï„)) â†’ Vector' EQuant Î“.length â†’ Term Ï„ â†’ Sort _
  | var {Î“ qs i} (hi : i < Î“.length) : qs.Var i â†’ WqD Î“ qs (.var i)
  | op {Î“ qs f a} : WqD Î“ qs a â†’ WqD Î“ qs (.op f a)
  | letâ‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b}
    : ql + qr â‰¤ qs â†’ WqD Î“ ql a
      â†’ WqD ((inferTy Î“ a)::Î“) (qr.cons (quant (inferTy Î“ a))) b
      â†’ WqD Î“ qs (.letâ‚ a b)
  | unit {Î“ qs} : 0 â‰¤ qs â†’ WqD Î“ qs .unit
  | pair {Î“} {ql qr qs : Vector' EQuant Î“.length} {a b}
    : ql + qr â‰¤ qs â†’ WqD Î“ ql a â†’ WqD Î“ qr b â†’ WqD Î“ qs (.pair a b)
  | letâ‚‚ {Î“} {ql qr qs : Vector' EQuant Î“.length} {a c}
    : ql + qr â‰¤ qs
    â†’ WqD Î“ ql a
    â†’ WqD ((inferLeft Î“ a)::(inferRight Î“ a)::Î“)
          ((qr.cons â†‘(quant (inferRight Î“ a))).cons â†‘(quant (inferLeft Î“ a)))
          c
    â†’ WqD Î“ qs (.letâ‚‚ a c)


-- TODO: WfqD â†’ Wf

-- TODO: WfqD â†ª WqD

-- TODO: { WqD | Wf } â†ª WfqD ==> { WqD | Wf } â‰… WfqD

inductive WfqM : (Î“ : List (Ty Ï„)) â†’ Vector' EQuant Î“.length â†’ Term Ï„ â†’ Ty Ï„ â†’ Prop
  | var {Î“ qs i A} (hi : i < Î“.length)
    : Î“[i] = A âˆ§ qs = Vector'.oneHot âŸ¨i, hiâŸ© 1 â†’ WfqM Î“ qs (.var i) A
  | op {Î“ qs f a A B} (hA : f.src = A) (hB : f.trg = B) : WfqM Î“ qs a A â†’ WfqM Î“ qs (.op f a) B
  | letâ‚ {Î“} {qs qs' : Vector' EQuant Î“.length} {qA a b A B}
    : WfqM Î“ qs a A â†’ qA = â†‘(quant A)
    â†’ WfqM (A::Î“) (qs'.cons qA) b B â†’ WfqM Î“ (qs + qs') (.letâ‚ a b) B
  | unit {Î“ qs} : qs = 0 â†’ WfqM Î“ qs .unit (ğŸ™_ _)
  | pair {Î“} {qs qs' : Vector' EQuant Î“.length} {a b A B}
    : WfqM Î“ qs a A â†’ WfqM Î“ qs' b B â†’ WfqM Î“ (qs + qs') (.pair a b) (A âŠ— B)
  | letâ‚‚ {Î“} {qs qs' : Vector' EQuant Î“.length} {qA qB a c A B C} : WfqM Î“ qs a (A âŠ— B)
    â†’ qA = â†‘(quant A) â†’ qB = â†‘(quant B)
    â†’ WfqM (B::A::Î“) ((qs.cons qA).cons qB) c C
    â†’ WfqM Î“ (qs + qs') (.letâ‚‚ a c) C

-- TODO: WfqM has a unique quantity q

-- TODO: Nonempty (WfqD qs) iff Wfq qs iff WfqM qs' and qs' â‰¤ qs

-- TODO: Wfq ==> Wf (and therefore WfqM, WfqD, etc...)

-- TODO: Wq/WqM using inferTy; WqM iff Wq and

section Effect

variable {Îµ} [EffectSignature Ï„ Îµ] [PartialOrder Îµ]

inductive WeL : (Î“ : List Îµ) â†’ Term Ï„ â†’ Îµ â†’ Prop
  | var {Î“ i e} (hi : i < Î“.length) : Î“[i] â‰¤ e â†’ WeL Î“ (.var i) e
  | op {Î“ f a e} : f.eff â‰¤ e â†’ WeL Î“ a e â†’ WeL Î“ (.op f a) e
  | letâ‚ {Î“ a b e} : WeL Î“ a e â†’ WeL (e::Î“) b e â†’ WeL Î“ (.letâ‚ a b) e
  | unit {Î“} : WeL Î“ .unit e
  | pair {Î“ a b e} : WeL Î“ a e â†’ WeL Î“ b e â†’ WeL Î“ (.pair a b) e
  | letâ‚‚ {Î“ a c e} : WeL Î“ a e â†’ WeL (e::e::Î“) c e â†’ WeL Î“ (.letâ‚‚ a c) e

end Effect
