import Mathlib.CategoryTheory.Monoidal.Free.Basic
import Discretion.Wk.List

import Discretion.SEC.Signature
import Discretion.SEC.Untyped

open CategoryTheory

open CategoryTheory.MonoidalCategory

namespace SEC

open FreeSignature

namespace Term

variable {Ï„} [FreeSignature Ï„]

inductive Wf : List (Ty Ï„) â†’ Term Ï„ â†’ Ty Ï„ â†’ Prop
  | var {Î“ i A} (hÎ“ : i < Î“.length) : Î“[i] = A â†’ Wf Î“ (.var i) A
  | op {Î“ f a A B} (hA : f.src = A) (hB : f.trg = B) : Wf Î“ a A â†’ Wf Î“ (.op f a) B
  | letâ‚ {Î“ a b A B} : Wf Î“ a A â†’ Wf (A::Î“) b B â†’ Wf Î“ (.letâ‚ a b) B
  | unit {Î“} : Wf Î“ .unit (ğŸ™_ _)
  | pair {Î“ a b A B} : Wf Î“ a A â†’ Wf Î“ b B â†’ Wf Î“ (.pair a b) (A âŠ— B)
  | letâ‚‚ {Î“ a c A B C} : Wf Î“ a (A âŠ— B) â†’ Wf (B::A::Î“) c C â†’ Wf Î“ (.letâ‚‚ a c) C

def inferTy (Î“ : List (Ty Ï„)) : Term Ï„ â†’ Ty Ï„
  | .var i => if h : i < Î“.length then Î“[i] else .unit
  | .op f a => f.trg
  | .letâ‚ a b => b.inferTy ((a.inferTy Î“)::Î“)
  | .unit => .unit
  | .pair a b => a.inferTy Î“ âŠ— b.inferTy Î“
  | .letâ‚‚ a b => match a.inferTy Î“ with
    | .tensor A B => b.inferTy (B::A::Î“)
    | _ => .unit

theorem Wf.inferTy_eq {Ï„} [FreeSignature Ï„] {Î“} {t : Term Ï„} {A} (hA : Wf Î“ t A)
  : t.inferTy Î“ = A := by induction hA <;> simp [inferTy, *]

theorem Wf.unique {Ï„} [FreeSignature Ï„] {Î“} {t : Term Ï„} {A B}
  (hA : Wf Î“ t A) (hB : Wf Î“ t B) : A = B := by rw [<-hA.inferTy_eq, <-hB.inferTy_eq]

def inferLeft (Î“ : List (Ty Ï„)) (a : Term Ï„) : Ty Ï„ := match a.inferTy Î“ with
  | .tensor A _ => A
  | _ => .unit

def inferRight (Î“ : List (Ty Ï„)) (a : Term Ï„) : Ty Ï„ := match a.inferTy Î“ with
  | .tensor _ B => B
  | _ => .unit

@[simp]
theorem Wf.var_lt_length {Î“ i A} (h : Wf (Ï„ := Ï„) Î“ (.var i) A)
  : i < Î“.length := by cases h; assumption

@[simp]
theorem Wf.var_getElem_eq {Î“ i A} (h : Wf (Ï„ := Ï„) Î“ (.var i) A)
  : Î“[i]'h.var_lt_length = A := by cases h; assumption

theorem Wf.op_src_eq {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : f.src = a.inferTy Î“ := by cases h with | op h _ ha => convert h; rw [ha.inferTy_eq]

theorem Wf.op_trg_eq {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : f.trg = B := by cases h with | op _ h _ => exact h

theorem Wf.op_arg {Î“ f a B} (h : Wf (Ï„ := Ï„) Î“ (.op f a) B)
  : Wf Î“ a f.src := by cases h with | op h _ ha => exact h â–¸ ha

theorem Wf.unit_ty_eq {Î“} (h : Wf (Ï„ := Ï„) Î“ .unit A) : A = .unit := by cases h; rfl

theorem Wf.pair_ty_eq {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C)
  : C = a.inferTy Î“ âŠ— b.inferTy Î“
  := by cases h with | pair ha hb => rw [ha.inferTy_eq, hb.inferTy_eq]

theorem Wf.pair_left {Î“ a b A B} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) (A âŠ— B))
  : Wf Î“ a A := by cases h; assumption

theorem Wf.pair_right {Î“ a b A B} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) (A âŠ— B))
  : Wf Î“ b B := by cases h; assumption

theorem Wf.pair_left' {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C) : Wf Î“ a (a.inferTy Î“)
  := (h.pair_ty_eq â–¸ h).pair_left

theorem Wf.pair_right' {Î“ a b C} (h : Wf (Ï„ := Ï„) Î“ (.pair a b) C) : Wf Î“ b (b.inferTy Î“)
  := (h.pair_ty_eq â–¸ h).pair_right

theorem Wf.letâ‚_bind {Î“ a b B} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚ a b) B)
  : Wf Î“ a (a.inferTy Î“) := by cases h with | letâ‚ ha hb => convert ha; rw [ha.inferTy_eq]

theorem Wf.letâ‚_expr {Î“ a b B} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚ a b) B)
  : Wf (a.inferTy Î“::Î“) b B := by cases h with | letâ‚ ha hb => convert hb; rw [ha.inferTy_eq]

theorem Wf.letâ‚‚_bind {Î“ a c C} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚‚ a c) C)
  : Wf Î“ a (a.inferLeft Î“ âŠ— a.inferRight Î“) := by
  cases h with | letâ‚‚ ha hb =>
    simp only [inferLeft, inferRight]
    split
    case h_1 t => rw [ha.inferTy_eq] at t; cases t; exact ha
    case h_2 t => exact (t _ _ ha.inferTy_eq).elim

theorem Wf.letâ‚‚_expr {Î“ a c C} (h : Wf (Ï„ := Ï„) Î“ (.letâ‚‚ a c) C)
  : Wf (a.inferRight Î“::a.inferLeft Î“::Î“) c C := by
  cases h with | letâ‚‚ ha hb =>
    simp only [inferLeft, inferRight]
    split
    case h_1 t => rw [ha.inferTy_eq] at t; cases t; exact hb
    case h_2 t => exact (t _ _ ha.inferTy_eq).elim

-- TODO: set as default eliminator?
@[elab_as_elim]
def Wf.induction' {motive : âˆ€ {Î“ t A}, Wf (Ï„ := Ï„) Î“ t A â†’ Sort u}
  (var : âˆ€ {Î“ i A} (hÎ“ : i < Î“.length) (hA : Î“[i] = A), motive (.var hÎ“ hA))
  (op : âˆ€ {Î“} {f : Untyped.Inst Ï„} {a A B} (hA : f.src = A) (hB : f.trg = B) (ha : Wf Î“ a A),
    motive ha â†’ motive (.op hA hB ha))
  (letâ‚ : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf (A::Î“) b B),
    motive ha â†’ motive hb â†’ motive (.letâ‚ ha hb))
  (unit : âˆ€ {Î“}, motive (Î“ := Î“) .unit)
  (pair : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf Î“ b B),
    motive ha â†’ motive hb â†’ motive (.pair ha hb))
  (letâ‚‚ : âˆ€ {Î“ a c A B C} (ha : Wf Î“ a (A âŠ— B)) (hc : Wf (B::A::Î“) c C),
    motive ha â†’ motive hc â†’ motive (.letâ‚‚ ha hc))
  {Î“ t A} (h : Wf Î“ t A) : motive h := match t, A, h with
  | .var _, _, h => var h.var_lt_length h.var_getElem_eq
  | .op _ _, _, h => op rfl h.op_trg_eq h.op_arg (induction' var op letâ‚ unit pair letâ‚‚ h.op_arg)
  | .letâ‚ _ _, _, h => letâ‚ h.letâ‚_bind h.letâ‚_expr
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚_bind)
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚_expr)
  | .unit, .unit, _ => unit
  | .pair _ _, .tensor _ _, h => pair h.pair_left h.pair_right
    (induction' var op letâ‚ unit pair letâ‚‚ h.pair_left)
    (induction' var op letâ‚ unit pair letâ‚‚ h.pair_right)
  | .letâ‚‚ _ _, _, h => letâ‚‚ h.letâ‚‚_bind h.letâ‚‚_expr
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚‚_bind)
    (induction' var op letâ‚ unit pair letâ‚‚ h.letâ‚‚_expr)

@[elab_as_elim]
def Wf.cases' {motive : âˆ€ {Î“ t A}, Wf (Ï„ := Ï„) Î“ t A â†’ Sort u}
  (var : âˆ€ {Î“ i A} (hÎ“ : i < Î“.length) (hA : Î“[i] = A), motive (.var hÎ“ hA))
  (op : âˆ€ {Î“} {f : Untyped.Inst Ï„} {a A B} (hA : f.src = A) (hB : f.trg = B) (ha : Wf Î“ a A),
    motive (.op hA hB ha))
  (letâ‚ : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf (A::Î“) b B),
    motive (.letâ‚ ha hb))
  (unit : âˆ€ {Î“}, motive (Î“ := Î“) .unit)
  (pair : âˆ€ {Î“ a b A B} (ha : Wf Î“ a A) (hb : Wf Î“ b B),
    motive (.pair ha hb))
  (letâ‚‚ : âˆ€ {Î“ a c A B C} (ha : Wf Î“ a (A âŠ— B)) (hc : Wf (B::A::Î“) c C),
    motive (.letâ‚‚ ha hc))
  {Î“ t A} (h : Wf Î“ t A) : motive h := match t, A, h with
  | .var _, _, h => var h.var_lt_length h.var_getElem_eq
  | .op _ _, _, h => op rfl h.op_trg_eq h.op_arg
  | .letâ‚ _ _, _, h => letâ‚ h.letâ‚_bind h.letâ‚_expr
  | .unit, .unit, _ => unit
  | .pair _ _, .tensor _ _, h => pair h.pair_left h.pair_right
  | .letâ‚‚ _ _, _, h => letâ‚‚ h.letâ‚‚_bind h.letâ‚‚_expr

theorem Wf.wk {Ï„} [FreeSignature Ï„] {Î“ Î”} {Ï : â„• â†’ â„•} (h : Î“.IsWk Î” Ï) {t : Term Ï„} {A}
  (h : Wf Î” t A) : Wf Î“ (t.wk Ï) A := by
  induction h generalizing Î“ Ï with
  | var hi => have h := h _ hi; constructor; rw [h.2]; assumption
  | _ => constructor <;> apply_assumption <;> simp [List.IsWk.lift_iff, *]

-- TODO: this only works for Ï injective...
-- theorem Wf.unwk  {Ï„} [FreeSignature Ï„] {Î“ Î”} {Ï : â„• â†’ â„•} (h : Î“.IsWk Î” Ï) {t : Term Ï„} {A}
--   (h : Wf Î“ (t.wk Ï) A) : Wf Î” t A := by
--   induction t generalizing Î“ Î” Ï A <;> cases h
--   case var hÎ“ ha => sorry
--   all_goals (constructor <;> apply_assumption <;> assumption)

theorem Wf.wk0 {Ï„} [FreeSignature Ï„] {Î“} {t : Term Ï„} {A B}
  (h : Wf Î“ t A) : Wf (B::Î“) (wk0 t) A := h.wk (by simp)

def Subst.Wf (Î“ : List (Ty Ï„)) (Ïƒ : Subst Ï„) (Î” : List (Ty Ï„)) : Prop
  := âˆ€i, (h : i < Î”.length) â†’ Term.Wf Î“ (Ïƒ i) (Î”[i])

@[simp]
theorem Subst.Wf.nil {Î“} : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ [] := by simp [Subst.Wf]

theorem Subst.Wf.head {Î“ Ïƒ Î” A} (h : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ (A::Î”)) : Term.Wf Î“ (Ïƒ 0) A
  := h 0 (by simp)

theorem Subst.Wf.tail {Î“ Ïƒ Î” A} (h : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ (A::Î”)) : Subst.Wf Î“ (Ïƒ âˆ˜ .succ) Î”
  := Î»i hi => h i.succ (by simp [hi])

theorem Subst.Wf.cons {Î“ a A Ïƒ Î”} (ha : Term.Wf Î“ a A) (hÏƒ : Subst.Wf Î“ Ïƒ Î”)
  : Subst.Wf (Ï„ := Ï„) Î“ (Î»n => Nat.casesOn n a Ïƒ) (A::Î”) := Î»i hi => by
  cases i with
  | zero => exact ha
  | succ i => exact hÏƒ i (by convert hi using 0; simp)

theorem Subst.Wf.cons' {Î“ Ïƒ Î” A} (ha : Term.Wf Î“ (Ïƒ 0) A) (hÏƒ : Subst.Wf Î“ (Ïƒ âˆ˜ .succ) Î”)
  : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ (A::Î”) := by convert cons ha hÏƒ using 1; funext n; cases n <;> rfl

theorem Subst.Wf.cons_iff {Î“ Ïƒ Î” A}
  : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ (A::Î”) â†” Term.Wf Î“ (Ïƒ 0) A âˆ§ Subst.Wf Î“ (Ïƒ âˆ˜ .succ) Î” :=
  âŸ¨Î»h => âŸ¨h.head, h.tailâŸ©, Î»âŸ¨h, h'âŸ© => h'.cons' hâŸ©

theorem Subst.Wf.lift {Î“ Ïƒ Î”} (h : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ Î”) : Subst.Wf (A::Î“) (Ïƒ.lift) (A::Î”)
  := Î»i hi => by cases i with
  | zero => constructor <;> simp
  | succ i => simp [(h i (by convert hi using 0; simp)).wk0]

-- theorem Subst.Wf.lift_tail {Î“ Ïƒ Î”} (h : Subst.Wf (A::Î“) (Ïƒ.lift) (A::Î”)) : Subst.Wf (Ï„ := Ï„) Î“ Ïƒ Î”
--   := Î»i hi => sorry

inductive LSubst.Wf : List (Ty Ï„) â†’ LSubst Ï„ â†’ List (Ty Ï„) â†’ Prop
  | nil {Î“} : LSubst.Wf Î“ [] []
  | cons {Î“ Ïƒ Î” A} (ha : Term.Wf Î“ a A) (hÏƒ : LSubst.Wf Î“ Ïƒ Î”)
    : LSubst.Wf Î“ (a::Ïƒ) (A::Î”)

-- TODO: LSubst.Wf iff Subst.Wf var

-- TODO: Subst.Wf iff LSubst.Wf ofFn
