import Mathlib.CategoryTheory.Monoidal.Category
import Discretion.Utils.Category

namespace CategoryTheory

open MonoidalCategory

namespace Monoidal

variable {C : Type _} [Category C] [MonoidalCategoryStruct C]

abbrev ltimes {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') : X âŠ— X' âŸ¶ Y âŠ— Y' := (f â–· X') â‰« (Y â— g)

abbrev rtimes {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') : X âŠ— X' âŸ¶ Y âŠ— Y' := (X â— g) â‰« (f â–· Y')

scoped infixr:81 " â‹‰ " => ltimes

scoped infixr:81 " â‹Š " => rtimes

class Central {X Y : C} (f : X âŸ¶ Y) : Prop where
  left_sliding : âˆ€{X' Y' : C}, âˆ€g : X' âŸ¶ Y', f â‹‰ g = f â‹Š g
  right_sliding : âˆ€{X' Y' : C}, âˆ€g : X' âŸ¶ Y', g â‹‰ f = g â‹Š f

@[reassoc]
theorem left_sliding {X Y X' Y' : C}
  (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [hf : Central f] : f â‹‰ g = f â‹Š g := Central.left_sliding g

@[reassoc]
theorem right_sliding {X Y X' Y' : C}
  (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [hg : Central g] : f â‹‰ g = f â‹Š g := Central.right_sliding f

@[reassoc]
theorem left_exchange {X Y X' Y' : C}
  (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [hf : Central f] : (f â–· X') â‰« (Y â— g) = (X â— g) â‰« (f â–· Y')
  := left_sliding f g

@[reassoc]
theorem right_exchange {X Y X' Y' : C}
  (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [hg : Central g] : (f â–· X') â‰« (Y â— g) = (X â— g) â‰« (f â–· Y')
  := right_sliding f g

-- TODO: in fact, everything is central in a _binoidal_ category with sliding; can use this
-- to make things nicer...

end Monoidal

class IsBinoidal (C: Type u) [Category C] [MonoidalCategoryStruct C] : Prop where
  tensorHom_def {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
    f âŠ— g = (f â–· Xâ‚‚) â‰« (Yâ‚ â— g) := by
      aesop_cat
  whiskerLeft_id : âˆ€ (X Y : C), X â— ğŸ™ Y = ğŸ™ (X âŠ— Y) := by
    aesop_cat
  id_whiskerRight : âˆ€ (X Y : C), ğŸ™ X â–· Y = ğŸ™ (X âŠ— Y) := by
    aesop_cat
  whiskerLeft_comp : âˆ€ {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z), W â— (f â‰« g) = (W â— f) â‰« (W â— g)
    := by aesop_cat
  whiskerRight_comp : âˆ€ {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z), (f â‰« g) â–· W = (f â–· W) â‰« (g â–· W)
    := by aesop_cat

class IsPremonoidal (C: Type u) [Category C] [MonoidalCategoryStruct C]
  extends IsBinoidal C : Prop where
  associator_central : âˆ€ {X Y Z : C}, Monoidal.Central (Î±_ X Y Z).hom := by aesop_cat
  leftUnitor_central : âˆ€ {X : C}, Monoidal.Central (Î»_ X).hom := by aesop_cat
  rightUnitor_central : âˆ€ {X : C}, Monoidal.Central (Ï_ X).hom := by aesop_cat
  /-- Naturality of the associator isomorphism: `(fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ â‰ƒ fâ‚ âŠ— (fâ‚‚ âŠ— fâ‚ƒ)` -/
  associator_naturality :
    âˆ€ {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ),
      ((fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ âŠ— (fâ‚‚ âŠ— fâ‚ƒ)) := by
    aesop_cat
  /--
  Naturality of the left unitor, commutativity of `ğŸ™_ C âŠ— X âŸ¶ ğŸ™_ C âŠ— Y âŸ¶ Y` and `ğŸ™_ C âŠ— X âŸ¶ X âŸ¶ Y`
  -/
  leftUnitor_naturality :
    âˆ€ {X Y : C} (f : X âŸ¶ Y), ğŸ™_ _ â— f â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f := by
    aesop_cat
  /--
  Naturality of the right unitor: commutativity of `X âŠ— ğŸ™_ C âŸ¶ Y âŠ— ğŸ™_ C âŸ¶ Y` and `X âŠ— ğŸ™_ C âŸ¶ X âŸ¶ Y`
  -/
  rightUnitor_naturality :
    âˆ€ {X Y : C} (f : X âŸ¶ Y), f â–· ğŸ™_ _ â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f := by
    aesop_cat
  /--
  The pentagon identity relating the isomorphism between `X âŠ— (Y âŠ— (Z âŠ— W))` and `((X âŠ— Y) âŠ— Z) âŠ— W`
  -/
  pentagon :
    âˆ€ W X Y Z : C,
      (Î±_ W X Y).hom â–· Z â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« W â— (Î±_ X Y Z).hom =
        (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom := by
    aesop_cat
  /--
  The identity relating the isomorphisms between `X âŠ— (ğŸ™_ C âŠ— Y)`, `(X âŠ— ğŸ™_ C) âŠ— Y` and `X âŠ— Y`
  -/
  triangle :
    âˆ€ X Y : C, (Î±_ X (ğŸ™_ _) Y).hom â‰« X â— (Î»_ Y).hom = (Ï_ X).hom â–· Y := by
    aesop_cat

class IsMonoidal (C : Type u) [Category C] [MonoidalCategoryStruct C] extends
  IsPremonoidal C : Prop where
  tensor_comp : âˆ€ {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C}
    (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚),
    (fâ‚ â‰« gâ‚) âŠ— (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := by aesop_cat
-- TODO: ideally, MonoidalCategory extends PremonoidalCategory.
-- TODO: also, MonoidalCategoryStruct would best extend BinoidalCategory, but that's just me...

namespace Monoidal

variable {C : Type _} [Category C]

def instMonoidalCategory [MonoidalCategory C] : IsMonoidal C where
  tensorHom_def := MonoidalCategory.tensorHom_def
  associator_central := âŸ¨Î»g => by simp [ltimes, rtimes], Î»g => by simp [ltimes, rtimes]âŸ©
  leftUnitor_central := âŸ¨Î»g => by simp [ltimes, rtimes], Î»g => by simp [ltimes, rtimes]âŸ©
  rightUnitor_central := âŸ¨Î»g => by simp [ltimes, rtimes], Î»g => by simp [ltimes, rtimes]âŸ©

variable [MonoidalCategoryStruct C]

theorem whiskerRight_comp_rtimes {X Y Z X' Y' : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : X' âŸ¶ Y') :
  f â–· X' â‰« g â‹Š h = f â‹‰ h â‰« g â–· Y' := by simp

theorem whiskerLeft_comp_ltimes {X Y Z X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') (h : Y âŸ¶ Z) :
  X' â— f â‰« g â‹‰ h = g â‹Š f â‰« Y' â— h := by simp

class IsStrict (C : Type u) [Category C] [MonoidalCategoryStruct C] : Prop where
  tensor_assoc : âˆ€ {X Y Z : C}, (X âŠ— Y) âŠ— Z = X âŠ— (Y âŠ— Z)
  unit_tensor : âˆ€ {X : C}, ğŸ™_ C âŠ— X = X
  tensor_unit : âˆ€ {X : C}, X âŠ— ğŸ™_ C = X
  associator_eq_id : âˆ€ {X Y Z : C}, (Î±_ X Y Z).hom = eq_hom tensor_assoc
  leftUnitor_eq_id : âˆ€ {X : C}, (Î»_ X).hom = eq_hom unit_tensor
  rightUnitor_eq_id : âˆ€ {X : C}, (Ï_ X).hom = eq_hom tensor_unit

section IsBinoidal

variable [IsBinoidal C]

@[simp]
theorem whiskerLeft_id {X Y : C} : X â— ğŸ™ Y = ğŸ™ (X âŠ— Y)
  := IsBinoidal.whiskerLeft_id _ _

@[simp]
theorem id_whiskerRight {X Y : C} : ğŸ™ X â–· Y = ğŸ™ (X âŠ— Y)
  := IsBinoidal.id_whiskerRight _ _

@[simp]
theorem whiskerLeft_eq_hom {X Y Z : C} (p : Y = Z) : X â— (eq_hom p) = eq_hom (by rw [p])
  := by cases p; simp

@[simp]
theorem eq_hom_whiskerRight {X Y Z : C} (p : X = Y) : eq_hom p â–· Z = eq_hom (by rw [p])
  := by cases p; simp

@[reassoc, simp]
theorem whiskerLeft_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  : W â— (f â‰« g) = (W â— f) â‰« (W â— g) := IsBinoidal.whiskerLeft_comp f g

@[reassoc, simp]
theorem whiskerRight_comp {X Y Z W : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z)
  : (f â‰« g) â–· W = (f â–· W) â‰« (g â–· W) :=  IsBinoidal.whiskerRight_comp f g

@[reassoc, simp]
theorem whiskerRight_comp_inv {X Y Z : C} (f : X âŸ¶ Y) [IsIso f]
  : f â–· Z â‰« inv f â–· Z = ğŸ™ (X âŠ— Z) := by simp [<-whiskerRight_comp]

@[reassoc, simp]
theorem whiskerRight_inv_comp {X Y Z : C} (f : X âŸ¶ Y) [IsIso f]
  : inv f â–· Z â‰« f â–· Z = ğŸ™ (Y âŠ— Z) := by simp [<-whiskerRight_comp]

@[reassoc, simp]
theorem whiskerLeft_comp_inv {X Y Z : C} (f : X âŸ¶ Y) [IsIso f]
  : Z â— f â‰« Z â— inv f = ğŸ™ (Z âŠ— X) := by simp [<-whiskerLeft_comp]

@[reassoc, simp]
theorem whiskerLeft_inv_comp {X Y Z : C} (f : X âŸ¶ Y) [IsIso f]
  : Z â— inv f â‰« Z â— f = ğŸ™ (Z âŠ— Y) := by simp [<-whiskerLeft_comp]

instance whiskerRight_isIso {X Y Z : C} (f : X âŸ¶ Y) [IsIso f] : IsIso (f â–· Z) where
  out := âŸ¨inv f â–· Z, whiskerRight_comp_inv f, whiskerRight_inv_comp fâŸ©

instance whiskerLeft_isIso {X Y Z : C} (f : X âŸ¶ Y) [IsIso f] : IsIso (Z â— f) where
  out := âŸ¨Z â— inv f, whiskerLeft_comp_inv f, whiskerLeft_inv_comp fâŸ©

@[simp]
theorem inv_whiskerRight {X Y Z : C} (f : X âŸ¶ Y) [IsIso f] : inv (f â–· Z) = inv f â–· Z
  := by aesop_cat

@[simp]
theorem inv_whiskerLeft {X Y Z : C} (f : X âŸ¶ Y) [IsIso f] : inv (Z â— f) = Z â— inv f
  := by aesop_cat

theorem inv_ltimes {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [IsIso f] [IsIso g] :
  inv (f â‹‰ g) = inv f â‹Š inv g := by simp

theorem inv_rtimes {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') [IsIso f] [IsIso g] :
  inv (f â‹Š g) = inv f â‹‰ inv g := by simp

-- TODO: tensorHom is iso, ltimes is iso, rtimes is iso

theorem tensorHom_def {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  f âŠ— g = (f â–· Xâ‚‚) â‰« (Yâ‚ â— g) := IsBinoidal.tensorHom_def f g

theorem tensor_eq_ltimes {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  f âŠ— g = f â‹‰ g := tensorHom_def f g

theorem tensor_eq_rtimes_left {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) [Central f] :
  f âŠ— g = f â‹Š g := by rw [tensor_eq_ltimes, left_sliding]

theorem tensor_eq_rtimes_right {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) [Central g] :
  f âŠ— g = f â‹‰ g := by rw [tensor_eq_ltimes, right_sliding]

instance IsIso.instTensor' {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) [IsIso f] [IsIso g] :
  IsIso (f âŠ— g) := by rw [tensor_eq_ltimes]; infer_instance

@[simp]
theorem inv_tensor_left {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚)
  [IsIso f] [IsIso g] [Central f] :
  inv (f âŠ— g) = inv f âŠ— inv g := by simp [tensorHom_def, left_sliding]

@[simp]
theorem inv_tensor_right {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚)
  [IsIso f] [IsIso g] [Central g] :
  inv (f âŠ— g) = inv f âŠ— inv g := by simp [tensorHom_def, right_sliding]

@[simp]
theorem tensor_id {X Y : C} : ğŸ™ X âŠ— ğŸ™ Y = ğŸ™ (X âŠ— Y) := by simp [tensorHom_def]

@[simp]
theorem tensorHom_eq_hom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (p : Xâ‚ = Yâ‚) (q : Xâ‚‚ = Yâ‚‚) :
  eq_hom p âŠ— eq_hom q = eq_hom (by rw [p, q]) := by cases p; cases q; simp

theorem id_tensorHom {X Y Z : C} (f : X âŸ¶ Y) : ğŸ™ Z âŠ— f = Z â— f := by simp [tensorHom_def]

theorem tensorHom_id {X Y Z : C} (f : X âŸ¶ Y) : f âŠ— ğŸ™ Z = f â–· Z := by simp [tensorHom_def]

@[simp]
theorem ltimes_id {X Y : C} : ğŸ™ X â‹‰ ğŸ™ Y = ğŸ™ (X âŠ— Y) := by simp [ltimes]

@[simp]
theorem rtimes_id {X Y : C} : ğŸ™ X â‹Š ğŸ™ Y = ğŸ™ (X âŠ— Y) := by simp [rtimes]

theorem comp_ltimes {X Y Z X' Y' : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : X' âŸ¶ Y') :
  (f â‰« g) â‹‰ h = f â–· X' â‰« g â‹‰ h := by simp [ltimes]

theorem ltimes_comp {X Y X' Y' Z' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') (h : Y' âŸ¶ Z') :
  f â‹‰ (g â‰« h) = f â‹‰ g â‰« Y â— h := by simp [ltimes]

theorem comp_rtimes {X Y Z X' Y' : C} (f : X âŸ¶ Y) (g : Y âŸ¶ Z) (h : X' âŸ¶ Y') :
  (f â‰« g) â‹Š h = f â‹Š h â‰« (g â–· Y') := by simp [rtimes]

theorem rtimes_comp {X Y X' Y' Z' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') (h : Y' âŸ¶ Z') :
  f â‹Š (g â‰« h) = X â— g â‰« f â‹Š h := by simp [rtimes]

@[simp]
instance Central.id {X : C} : Central (ğŸ™ X) where
  left_sliding := by simp [ltimes, rtimes]
  right_sliding := by simp [ltimes, rtimes]

@[simp]
instance Central.eq_hom {X Y : C} (p : X = Y) : Central (eq_hom p) := by cases p; simp

instance Central.comp {X Y Z : C} {f : X âŸ¶ Y} {g : Y âŸ¶ Z}
  [hf : Central f] [hg : Central g] : Central (f â‰« g) where
  left_sliding h := by
    simp only [ltimes, whiskerRight_comp, Category.assoc, left_exchange g h, rtimes]
    rw [<-Category.assoc, left_exchange f h, Category.assoc]
  right_sliding h := by
    simp only [ltimes, whiskerLeft_comp, rtimes, Category.assoc, <-right_exchange h g]
    rw [<-Category.assoc, right_exchange h f, Category.assoc]

instance Central.inv {X Y : C} {f : X âŸ¶ Y} [IsIso f] [Central f] : Central (inv f) where
  left_sliding g := (cancel_epi (f â–· _)).mp (by
    simp only [â† Category.assoc, whiskerRight_comp_inv, Category.id_comp, left_exchange]
    simp)
  right_sliding g := (cancel_epi (_ â— f)).mp (by
    rw [whiskerLeft_comp_ltimes, <-right_sliding g]
    simp only [rtimes, â† Category.assoc, whiskerLeft_comp_inv, Category.id_comp]
    simp)

instance Central.inv_hom {X Y : C} {f : X â‰… Y} [hf : Central f.hom] : Central f.inv := by
  convert Central.inv (f := f.hom)
  simp

theorem Central.hom_inv {X Y : C} {f : X â‰… Y} [hf : Central f.inv] : Central f.hom := by
  convert Central.inv (f := f.inv)
  simp

end IsBinoidal

section IsStrict

variable [IsStrict C]

theorem tensor_assoc {X Y Z : C} : (X âŠ— Y) âŠ— Z = X âŠ— (Y âŠ— Z) := IsStrict.tensor_assoc

theorem unit_tensor {X : C} : ğŸ™_ C âŠ— X = X := IsStrict.unit_tensor

theorem tensor_unit {X : C} : X âŠ— ğŸ™_ C = X := IsStrict.tensor_unit

theorem associator_eq_id {X Y Z : C} : (Î±_ X Y Z).hom = eq_hom tensor_assoc
  := IsStrict.associator_eq_id

theorem leftUnitor_eq_id {X : C} : (Î»_ X).hom = eq_hom unit_tensor := IsStrict.leftUnitor_eq_id

theorem rightUnitor_eq_id {X : C} : (Ï_ X).hom = eq_hom tensor_unit := IsStrict.rightUnitor_eq_id

section IsBinoidal

variable [IsBinoidal C]

theorem associator_central_of_strict {X Y Z : C} : Central (Î±_ X Y Z).hom
  := by simp [associator_eq_id]

theorem leftUnitor_central_of_strict {X : C} : Central (Î»_ X).hom
  := by simp [leftUnitor_eq_id]

theorem rightUnitor_central_of_strict {X : C} : Central (Ï_ X).hom
  := by simp [rightUnitor_eq_id]

theorem pentagon_of_strict {W X Y Z : C} :
  (Î±_ W X Y).hom â–· Z â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« W â— (Î±_ X Y Z).hom =
    (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom
  := by simp [associator_eq_id]

theorem triangle_of_strict {X Y : C} :
  (Î±_ X (ğŸ™_ _) Y).hom â‰« X â— (Î»_ Y).hom = (Ï_ X).hom â–· Y
  := by simp [associator_eq_id, leftUnitor_eq_id, rightUnitor_eq_id]

end IsBinoidal

end IsStrict

section IsPremonoidal

variable [IsPremonoidal C]

theorem associator_naturality {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  ((fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).hom â‰« (fâ‚ âŠ— (fâ‚‚ âŠ— fâ‚ƒ))
  := IsPremonoidal.associator_naturality fâ‚ fâ‚‚ fâ‚ƒ

theorem associator_naturality_right {X Y Z : C} (f : X âŸ¶ X') :
  (f â–· Y) â–· Z â‰« (Î±_ X' Y Z).hom = (Î±_ X Y Z).hom â‰« f â–· (Y âŠ— Z) := by
  convert associator_naturality f (ğŸ™ Y) (ğŸ™ Z) using 1 <;> simp [tensorHom_def]

theorem associator_naturality_middle {X Y Z : C} (f : Y âŸ¶ Y') :
  (X â— f) â–· Z â‰« (Î±_ X Y' Z).hom = (Î±_ X Y Z).hom â‰« X â— (f â–· Z) := by
  convert associator_naturality (ğŸ™ _) f (ğŸ™ _) using 1 <;> simp [tensorHom_def]

theorem associator_naturality_left {X Y Z : C} (f : Z âŸ¶ Z') :
  (X âŠ— Y) â— f â‰« (Î±_ X Y Z').hom = (Î±_ X Y Z).hom â‰« X â— (Y â— f) := by
  convert associator_naturality (ğŸ™ _) (ğŸ™ _) f using 1 <;> simp [tensorHom_def]

theorem associator_inv_naturality
  {Xâ‚ Xâ‚‚ Xâ‚ƒ Yâ‚ Yâ‚‚ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  (fâ‚ âŠ— (fâ‚‚ âŠ— fâ‚ƒ)) â‰« (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).inv = (Î±_ Xâ‚ Xâ‚‚ Xâ‚ƒ).inv â‰« ((fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ)
  := (cancel_mono (Î±_ Yâ‚ Yâ‚‚ Yâ‚ƒ).hom).mp (by simp [associator_naturality])

theorem associator_inv_naturality_right {X Y Z : C} (f : X âŸ¶ X') :
  f â–· (Y âŠ— Z) â‰« (Î±_ X' Y Z).inv = (Î±_ X Y Z).inv â‰« f â–· Y â–· Z := by
  convert associator_inv_naturality f (ğŸ™ _) (ğŸ™ _) using 1 <;> simp [tensorHom_def]

theorem associator_inv_naturality_middle {X Y Z : C} (f : Y âŸ¶ Y') :
  X â— (f â–· Z) â‰« (Î±_ X Y' Z).inv = (Î±_ X Y Z).inv â‰« (X â— f) â–· Z := by
  convert associator_inv_naturality (ğŸ™ _) f (ğŸ™ _) using 1 <;> simp [tensorHom_def]

theorem associator_inv_naturality_left {X Y Z : C} (f : Z âŸ¶ Z') :
  X â— (Y â— f) â‰« (Î±_ X Y Z').inv = (Î±_ X Y Z).inv â‰« (X âŠ— Y) â— f := by
  convert associator_inv_naturality (ğŸ™ _) (ğŸ™ _) f using 1 <;> simp [tensorHom_def]

@[reassoc]
theorem leftUnitor_naturality {X Y : C} (f : X âŸ¶ Y) :
  ğŸ™_ C â— f â‰« (Î»_ Y).hom = (Î»_ X).hom â‰« f := IsPremonoidal.leftUnitor_naturality f

@[reassoc]
theorem leftUnitor_inv_naturality {X Y : C} (f : X âŸ¶ Y) :
  f â‰« (Î»_ Y).inv = (Î»_ X).inv â‰« ğŸ™_C â— f := by
  apply (cancel_mono (Î»_ Y).hom).mp
  simp [leftUnitor_naturality]

@[reassoc]
theorem rightUnitor_naturality {X Y : C} (f : X âŸ¶ Y) :
  f â–· ğŸ™_ C â‰« (Ï_ Y).hom = (Ï_ X).hom â‰« f := IsPremonoidal.rightUnitor_naturality f

@[reassoc]
theorem rightUnitor_inv_naturality {X Y : C} (f : X âŸ¶ Y) :
  f â‰« (Ï_ Y).inv = (Ï_ X).inv â‰« f â–· ğŸ™_ C := by
  apply (cancel_mono (Ï_ Y).hom).mp
  simp [rightUnitor_naturality]

section IsStrict

variable [IsStrict C]

theorem tensorHom_assoc {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ Xâ‚ƒ Yâ‚ƒ : C} (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (fâ‚ƒ : Xâ‚ƒ âŸ¶ Yâ‚ƒ) :
  HEq ((fâ‚ âŠ— fâ‚‚) âŠ— fâ‚ƒ) (fâ‚ âŠ— (fâ‚‚ âŠ— fâ‚ƒ)) := by
  rw [<-heq_iff_eq_hom_right_left]
  convert associator_naturality fâ‚ fâ‚‚ fâ‚ƒ using 1 <;> simp [associator_eq_id]
  all_goals simp [tensor_assoc]

theorem whiskerRight_whiskerRight_heq {X Y Z Z' : C} {f : Z âŸ¶ Z'}
  : HEq (f â–· X â–· Y) (f â–· (X âŠ— Y)) := by
  rw [<-heq_iff_eq_hom_right_left]
  convert associator_naturality_right f using 1 <;> simp [associator_eq_id]
  all_goals simp [tensor_assoc]

theorem whiskerRight_whiskerLeft_heq {X Y Z Z' : C} {f : Z âŸ¶ Z'}
  : HEq ((X â— f) â–· Y) (X â— f â–· Y) := by
  rw [<-heq_iff_eq_hom_right_left]
  convert associator_naturality_middle f using 1 <;> simp [associator_eq_id]
  all_goals simp [tensor_assoc]

theorem whiskerLeft_whiskerLeft_heq {X Y Z Z' : C} {f : Z âŸ¶ Z'}
  : HEq ((X âŠ— Y) â— f) (X â— Y â— f) := by
  rw [<-heq_iff_eq_hom_right_left]
  convert associator_naturality_left f using 1 <;> simp [associator_eq_id]
  all_goals simp [tensor_assoc]

theorem unit_whiskerLeft_heq {X Y : C} (f : X âŸ¶ Y) : HEq (ğŸ™_ C â— f) f := by
  rw [<-heq_iff_eq_hom_right_left]
  convert leftUnitor_naturality f using 1 <;> simp [leftUnitor_eq_id]
  all_goals simp [unit_tensor]

theorem whiskerRight_unit_heq {X Y : C} (f : X âŸ¶ Y) : HEq (f â–· ğŸ™_ C) f := by
  rw [<-heq_iff_eq_hom_right_left]
  convert rightUnitor_naturality f using 1 <;> simp [rightUnitor_eq_id]
  all_goals simp [tensor_unit]

end IsStrict

theorem pentagon {W X Y Z : C} :
  (Î±_ W X Y).hom â–· Z â‰« (Î±_ W (X âŠ— Y) Z).hom â‰« W â— (Î±_ X Y Z).hom =
    (Î±_ (W âŠ— X) Y Z).hom â‰« (Î±_ W X (Y âŠ— Z)).hom := IsPremonoidal.pentagon W X Y Z

theorem triangle {X Y : C} : (Î±_ X (ğŸ™_ _) Y).hom â‰« X â— (Î»_ Y).hom = (Ï_ X).hom â–· Y
  := IsPremonoidal.triangle X Y

-- TODO: interactions with ltimes, rtimes...

instance Central.whiskerRight {X Y Z : C} (f : X âŸ¶ Y) [hf : Central f] : Central (f â–· Z) where
  left_sliding g := by
    rw [ltimes]
    apply (cancel_mono (Î±_ _ _ _).hom).mp
    rw [
      Category.assoc, associator_naturality_left, <-Category.assoc,
      associator_naturality_right, Category.assoc, left_exchange,
      <-Category.assoc, <-associator_naturality_left, Category.assoc,
      <-associator_naturality_right, Category.assoc
    ]
  right_sliding g := by
    rw [ltimes]
    apply (cancel_mono (Î±_ _ _ _).inv).mp
    rw [
      Category.assoc, associator_inv_naturality_middle, <-Category.assoc,
      associator_inv_naturality_right, Category.assoc, <-whiskerRight_comp, right_exchange,
      whiskerRight_comp, <-Category.assoc, <-associator_inv_naturality_middle, Category.assoc,
      <-associator_inv_naturality_right, rtimes, Category.assoc
    ]

instance Central.whiskerLeft {X Y Z : C} (f : X âŸ¶ Y) [hf : Central f] : Central (Z â— f) where
  left_sliding g := by
    rw [ltimes]
    apply (cancel_mono (Î±_ _ _ _).hom).mp
    rw [
      Category.assoc, associator_naturality_left, <-Category.assoc,
      associator_naturality_middle, Category.assoc, <-whiskerLeft_comp, left_exchange,
      whiskerLeft_comp, <-Category.assoc, <-associator_naturality_left, Category.assoc,
      <-associator_naturality_middle, rtimes, Category.assoc
    ]
  right_sliding g := by
    rw [rtimes]
    apply (cancel_mono (Î±_ _ _ _).inv).mp
    rw [
      Category.assoc, associator_inv_naturality_left, <-Category.assoc,
      associator_inv_naturality_right, Category.assoc, right_exchange,
      <-Category.assoc, <-associator_inv_naturality_left, Category.assoc,
      <-associator_inv_naturality_right, Category.assoc
    ]

instance Central.tensorHom {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚)
  [hf : Central f] [hg : Central g] : Central (f âŠ— g) := by rw [tensorHom_def]; infer_instance

instance associator_central {X Y Z : C} : Central (Î±_ X Y Z).hom := IsPremonoidal.associator_central

theorem associator_inv_central {X Y Z : C} : Central (Î±_ X Y Z).inv := inferInstance

instance leftUnitor_central {X : C} : Central (Î»_ X).hom := IsPremonoidal.leftUnitor_central

theorem leftUnitor_inv_central {X : C} : Central (Î»_ X).inv := inferInstance

instance rightUnitor_central {X : C} : Central (Ï_ X).hom := IsPremonoidal.rightUnitor_central

theorem rightUnitor_inv_central {X : C} : Central (Ï_ X).inv := inferInstance

theorem tensor_comp_left {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) [Central gâ‚] :
  (fâ‚ â‰« gâ‚) âŠ— (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := by
    simp only [tensorHom_def, whiskerRight_comp, whiskerLeft_comp, <-Category.assoc]
    apply congrArgâ‚‚ _ _ rfl
    simp only [Category.assoc, left_exchange gâ‚ fâ‚‚]

theorem tensor_comp_right {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) [Central fâ‚‚] :
  (fâ‚ â‰« gâ‚) âŠ— (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := by
    simp only [tensorHom_def, whiskerRight_comp, whiskerLeft_comp, <-Category.assoc]
    apply congrArgâ‚‚ _ _ rfl
    simp only [Category.assoc, right_exchange gâ‚ fâ‚‚]

instance IsMonoidal.of_all_central
  [all_central : âˆ€{X Y : C}, âˆ€f : X âŸ¶ Y, Central f] : IsMonoidal C where
  tensor_comp fâ‚ fâ‚‚ gâ‚ gâ‚‚ := have h := all_central gâ‚; by rw [tensor_comp_left]

end IsPremonoidal

section IsMonoidal

variable [IsMonoidal C]

theorem tensor_comp {Xâ‚ Yâ‚ Zâ‚ Xâ‚‚ Yâ‚‚ Zâ‚‚ : C}
  (fâ‚ : Xâ‚ âŸ¶ Yâ‚) (fâ‚‚ : Xâ‚‚ âŸ¶ Yâ‚‚) (gâ‚ : Yâ‚ âŸ¶ Zâ‚) (gâ‚‚ : Yâ‚‚ âŸ¶ Zâ‚‚) :
  (fâ‚ â‰« gâ‚) âŠ— (fâ‚‚ â‰« gâ‚‚) = (fâ‚ âŠ— fâ‚‚) â‰« (gâ‚ âŠ— gâ‚‚) := IsMonoidal.tensor_comp fâ‚ fâ‚‚ gâ‚ gâ‚‚

theorem whisker_exchange {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y')
  : f â–· X' â‰« Y â— g = X â— g â‰« f â–· Y'
  := by simp [<-tensorHom_id, <-id_tensorHom, <-tensor_comp]

theorem sliding {X Y X' Y' : C} (f : X âŸ¶ Y) (g : X' âŸ¶ Y') : f â‹‰ g = f â‹Š g
  := whisker_exchange f g

@[simp]
instance Central.monoidal {X Y : C} (f : X âŸ¶ Y) : Central f where
  left_sliding g := sliding f g
  right_sliding g := sliding g f

theorem tensor_eq_rtimes {Xâ‚ Yâ‚ Xâ‚‚ Yâ‚‚ : C} (f : Xâ‚ âŸ¶ Yâ‚) (g : Xâ‚‚ âŸ¶ Yâ‚‚) :
  f âŠ— g = f â‹Š g := by rw [<-sliding, tensor_eq_ltimes]

end IsMonoidal

end Monoidal

end CategoryTheory
