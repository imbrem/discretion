import Mathlib.CategoryTheory.Limits.Shapes.BinaryProducts

import Discretion.MorphismProperty.BinaryProducts

import Discretion.AddMonoidalCategory.ChosenCoproducts

namespace CategoryTheory

open Limits

open MorphismProperty

class Iterate (C : Type u) [Category C] [HasBinaryCoproducts C] where
  iterate {X Y : C} : (X âŸ¶ Y â¨¿ X) â†’ (X âŸ¶ Y)
  fixpoint {X Y : C} {f : X âŸ¶ Y â¨¿ X}
    : f â‰« coprod.desc (ğŸ™ Y) (iterate f) = iterate f

def iterate {C : Type u} [Category C] [HasBinaryCoproducts C] [Iterate C] {X Y : C}
  : (X âŸ¶ Y â¨¿ X) â†’ (X âŸ¶ Y) := Iterate.iterate

class Iterate.Uniform (C : Type u) [Category C] [HasBinaryCoproducts C] [Iterate C]
  (W : MorphismProperty C) : Prop where
  uniform {X Y : C} {f : Y âŸ¶ Z â¨¿ Y} {g : X âŸ¶ Z â¨¿ X} {h : X âŸ¶ Y}
    : W h â†’ h â‰« f = g â‰« coprod.map (ğŸ™ Z) h â†’ h â‰« iterate f = iterate g

-- Part 1 of Lemma 31 of Goncharov and SchrÃ¶der (2018, Guarded Traced Categories)
theorem Iterate.Uniform.squaring {C : Type u} [Category C] [HasBinaryCoproducts C] [Iterate C]
  {W : MorphismProperty C} [W.ContainsBinaryCoproducts] [U : Iterate.Uniform C W]
  (codiagonal : âˆ€{X Y : C} {f : X âŸ¶ (Y â¨¿ X) â¨¿ X},
    iterate (iterate f) = iterate (f â‰« coprod.desc (ğŸ™ (Y â¨¿ X)) coprod.inr))
  {X Y : C} {f : X âŸ¶ Y â¨¿ X} : iterate (f â‰« coprod.desc coprod.inl f) = iterate f := by
  generalize hw
    : (
      coprod.desc
        (f â‰« coprod.map (ğŸ™ Y) coprod.inr â‰« coprod.inl)
        (f â‰« coprod.map coprod.inl coprod.inl)
      : X â¨¿ X âŸ¶ (Y â¨¿ (X â¨¿ X)) â¨¿ (X â¨¿ X)
    )
    = w
  calc
  _ = coprod.inr â‰« iterate (iterate w) := by
    have u
      : coprod.inr â‰« iterate w = (f â‰« coprod.desc coprod.inl f) â‰« coprod.map (ğŸ™ Y) coprod.inr :=
      calc
      _ = coprod.inr â‰« w â‰« coprod.desc (ğŸ™ _) (iterate w) := by rw [fixpoint]
      _ = f â‰« coprod.desc coprod.inl (coprod.inl â‰« iterate w) := by simp [<-hw]
      _ = f â‰« coprod.desc coprod.inl (coprod.inl â‰« w â‰« coprod.desc (ğŸ™ _) (iterate w))
        := by rw [fixpoint]
      _ = f â‰« coprod.desc coprod.inl f â‰« coprod.map (ğŸ™ _) coprod.inr := by simp [<-hw]
      _ = _ := by rw [Category.assoc]
      ;
    rw [U.uniform inr_mem u]
  _ = coprod.inr â‰« iterate (w â‰« coprod.desc (ğŸ™ _) coprod.inr) := by rw [codiagonal]
  _ = coprod.inr
      â‰« iterate (coprod.desc (f â‰« coprod.map (ğŸ™ Y) coprod.inr) (f â‰« coprod.map (ğŸ™ Y) coprod.inl))
        := by simp [<-hw, <-coprod.desc_comp_inl_comp_inr]
  _ = coprod.inr â‰« codiag _ â‰« iterate f := by
    have u
      : codiag _ â‰« f
      = coprod.desc
          (f â‰« coprod.map (ğŸ™ Y) coprod.inr)
          (f â‰« coprod.map (ğŸ™ Y) coprod.inl)
        â‰« coprod.map (ğŸ™ _) (codiag _)
      := by simp
    rw [U.uniform codiag_mem u]
  _ = _ := by simp

-- Part 2 of Lemma 32 of Goncharov and SchrÃ¶der (2018, Guarded Traced Categories)
theorem Iterate.Uniform.dinaturality {C : Type u} [Category C] [HasBinaryCoproducts C] [Iterate C]
  {W : MorphismProperty C} [W.ContainsBinaryCoproducts] [U : Iterate.Uniform C W]
  (squaring : âˆ€{X Y : C} {f : X âŸ¶ Y â¨¿ X}, iterate (f â‰« coprod.desc coprod.inl f) = iterate f)
  {X Y Z : C} {f : X âŸ¶ Y â¨¿ Z} {g : Z âŸ¶ Y â¨¿ X}
  : f â‰« coprod.desc (ğŸ™ Y) (iterate (g â‰« coprod.desc coprod.inl f))
      = iterate (f â‰« coprod.desc coprod.inl g)
  := by
  generalize hh
    : coprod.desc (f â‰« coprod.map (ğŸ™ Y) coprod.inr) (g â‰« coprod.map (ğŸ™ Y) coprod.inl) = h
  have h1 : coprod.inl â‰« iterate h = iterate (f â‰« coprod.desc coprod.inl g)
    := by
    rw [<-squaring (f := h)]
    apply U.uniform inl_mem
    simp [<-hh]
  have h2 : coprod.inr â‰« iterate h = iterate (g â‰« coprod.desc coprod.inl f)
    := by
    rw [<-squaring (f := h)]
    apply U.uniform inr_mem
    simp [<-hh]
  apply Eq.symm
  calc
    _ = coprod.inl â‰« iterate h := h1.symm
    _ = coprod.inl â‰« h â‰« coprod.desc (ğŸ™ _) (iterate h) := by rw [fixpoint]
    _ = f â‰« coprod.desc (ğŸ™ _) (coprod.inr â‰« iterate h) := by simp [<-hh]
    _ = _ := by rw [h2]

class Iterate.Conway (C : Type u) [Category C] [HasBinaryCoproducts C] [Iterate C] : Prop
  where
  naturality {X Y Z : C} {f : X âŸ¶ Y â¨¿ X} {g : Y âŸ¶ Z}
    : iterate (f â‰« coprod.map g (ğŸ™ X)) = (iterate f) â‰« g
  dinaturality {X Y Z : C} {f : X âŸ¶ Y â¨¿ Z} {g : Z âŸ¶ Y â¨¿ X}
    : f â‰« coprod.desc (ğŸ™ Y) (iterate (g â‰« coprod.desc coprod.inl f))
      = iterate (f â‰« coprod.desc coprod.inl g)
  codiagonal {X Y : C} {f : X âŸ¶ (Y â¨¿ X) â¨¿ X}
    : iterate (iterate f) = iterate (f â‰« coprod.desc (ğŸ™ (Y â¨¿ X)) coprod.inr)

theorem Iterate.Uniform.conway {C : Type u} [Category C] [HasBinaryCoproducts C] [Iterate C]
  {W : MorphismProperty C} [W.ContainsBinaryCoproducts] [U : Iterate.Uniform C W]
  (naturality : âˆ€{X Y Z : C} {f : X âŸ¶ Y â¨¿ X} {g : Y âŸ¶ Z},
    iterate (f â‰« coprod.map g (ğŸ™ X)) = (iterate f) â‰« g)
  (codiagonal : âˆ€{X Y : C} {f : X âŸ¶ (Y â¨¿ X) â¨¿ X},
    iterate (iterate f) = iterate (f â‰« coprod.desc (ğŸ™ (Y â¨¿ X)) coprod.inr))
  : Iterate.Conway C where
  naturality := naturality
  codiagonal := codiagonal
  dinaturality := U.dinaturality (U.squaring codiagonal)

end CategoryTheory
